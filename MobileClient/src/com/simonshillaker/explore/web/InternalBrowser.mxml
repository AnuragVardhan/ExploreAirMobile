<?xml version="1.0" encoding="utf-8"?>
<core:BackButtonView xmlns:core="com.simonshillaker.explore.core.*"
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		creationComplete="onCreationComplete(event)" 
		remove="onRemove()" 
		tabBarVisible="false"
		title="{ResourceBundleUtil.INTERNAL_BROWSER_TITLE}">
	
	<fx:Script>
		<![CDATA[
			import com.simonshillaker.explore.core.PageInfo;
			import com.simonshillaker.explore.core.ResourceBundleUtil;
			import com.simonshillaker.explore.core.SourceURLs;
			
			import mx.events.FlexEvent;
			import mx.resources.IResourceManager;
			import mx.resources.ResourceManager;
			
			[Bindable]
			public var webView:StageWebView;
			
			[Bindable]
			private var isLoading:Boolean = true;
			
			[Bindable]
			private var isError:Boolean = false;
			
			private var isViewSideways:Boolean = false;
			
			private function onCreationComplete(event:FlexEvent):void 
			{
				info = new PageInfo(title,
					ResourceBundleUtil.INTERNAL_BROWSER_INFO,
					SourceURLs.WEB);
				
				if(!StageWebView.isSupported) {
					errorLabel.text = ResourceBundleUtil.INTERNAL_BROWSER_NOT_SUPPORTED;
					this.enabled = false;
				}
				
				webView = new StageWebView();
				webView.loadURL(ResourceBundleUtil.WEB_DEFAULT_URL);
				webView.addEventListener(ErrorEvent.ERROR, handleWebError);
				webView.addEventListener(Event.COMPLETE, handleWebSuccess);
				
				stage.addEventListener(StageOrientationEvent.ORIENTATION_CHANGE, handleOrientationChange);
				
			}
			
			private function sizeWebView():void
			{
				var placeHolderXY:Point = new Point(webViewPlaceholder.x, webViewPlaceholder.y);
				var webViewXY:Point = browserContainer.localToGlobal(placeHolderXY); 
				
				webView.viewPort = new Rectangle(webViewXY.x, 
					webViewXY.y, 
					webViewPlaceholder.width, 
					webViewPlaceholder.height);
			}
			
			protected function handleOrientationChange(event:StageOrientationEvent):void
			{
				sizeWebView();
			}
			
			private function handleWebError(event:ErrorEvent):void
			{
				isLoading = false
				isError = true;
			}
			
			private function handleWebSuccess(event:Event):void
			{
				webView.stage = stage;
				
				if(!webView.viewPort)
				{
					sizeWebView();
				}
				
				isLoading = false;
				isError = false;
			}
			
			protected function goToUrl():void
			{
				if(webView)
				{
					webView.stop();
					webView.stage = null;
					isError = false;
					isLoading = true;
					webView.loadURL(urlInput.text);
				}
			}
			
			private function historyBack():void
			{
				if(webView && webView.isHistoryBackEnabled)
				{
					isLoading = true;
					isError = false;
					webView.historyBack();
				}
			}
			
			private function historyForward():void
			{
				if(webView && webView.isHistoryForwardEnabled)
				{
					isLoading = true;
					isError = false;
					webView.historyForward();
				}
			}
			
			private function onRemove():void
			{
				webView.dispose();
				isLoading = false;
				isError = false;
			}
			
			override protected function get resourceManager():IResourceManager
			{
				return ResourceManager.getInstance();
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup id="browserContainer" width="100%" height="100%" >
		<s:HGroup id="navBar" width="100%">
			<s:Button id="historyBackButton" width="40" height="40"
					  label="{ResourceBundleUtil.NAV_BACK_LABEL}"
					  click="historyBack()"/>
			
			<s:Button id="historyForwardButton" width="40" height="40"
					  label="{ResourceBundleUtil.NAV_FORWARD_LABEL}"
					  click="historyForward()"/>
			
			<s:TextInput id="urlInput" width="100%" height="40" enter="goToUrl()" prompt="Enter a URL"/>
			
			<s:Button id="goToURLButton" width="60" height="40" label="Go" click="goToUrl()"/>
			
		</s:HGroup>
		
		<s:Group id="webViewPlaceholder" width="100%" height="100%" />
	</s:VGroup>
	
	<s:BusyIndicator visible="{isLoading}" horizontalCenter="0" verticalCenter="0"/>
	
	<s:Label id="errorLabel" visible="{isError}" horizontalCenter="0"
			 text="{ResourceBundleUtil.INTERNAL_BROWSER_INVALID_URL}"
			 verticalCenter="0"/>

</core:BackButtonView>