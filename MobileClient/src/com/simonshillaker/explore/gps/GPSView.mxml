<?xml version="1.0" encoding="utf-8"?>
<core:VerticalCenteredViewWithBackButton xmlns:core="com.simonshillaker.explore.core.*"
		xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" 
		title="{resourceManager.getString('explore_air_mobile', 'gps_title')}"
		remove="onRemove()">

	<fx:Script>
		<![CDATA[
			import com.google.maps.LatLng;
			import com.google.maps.Map;
			import com.google.maps.MapEvent;
			import com.google.maps.MapType;
			import com.google.maps.controls.ScaleControl;
			import com.google.maps.controls.ZoomControl;
			import com.simonshillaker.explore.maps.GoogleMapsConstants;
			
			import flash.sensors.Geolocation;
			
			import mx.events.FlexEvent;
			
			private var geolocation:Geolocation;
			
			private var map:Map;
			
			private var latitude:Number;
			private var longitude:Number;
			private var heading:Number;
			private var altitude:Number;
			private var speed:Number;
			
			[Bindable]
			private var infoText:String = "";

			[Bindable]
			private var isGpsSupported:Boolean = true;
			[Bindable]
			private var isMapReady:Boolean = false;
			[Bindable]
			private var isBusy:Boolean = false;
			
			protected function initialise():void
			{
				isBusy = true;
				
				isGpsSupported = initialiseGeolocation();
				
				if(!isGpsSupported)
				{
					isBusy = false;
					return;
				}
				
				validateNow();
				
				initialiseMap();
			}
			
			private function initialiseGeolocation():Boolean
			{
				if(Geolocation.isSupported)
				{
					geolocation = new Geolocation();
					
					if(geolocation.muted)
					{
						infoText = resourceManager.getString('explore_air_mobile', 'gps_not_enabled');
						return false;
					}
					
					geolocation.addEventListener(GeolocationEvent.UPDATE, handleGeolocationUpdate);
					return true;
				}
				else 
				{
					infoText = resourceManager.getString('explore_air_mobile', 'gps_not_supported');
					return false;
				}
				
			}

			protected function handleGeolocationUpdate(event:GeolocationEvent):void
			{
				latitude = event.latitude;
				longitude = event.longitude;
				heading = event.heading;
				altitude = event.altitude;
				speed= event.speed;
				
				if(isMapReady)
				{
					map.setCenter(new LatLng(latitude, longitude), 14, MapType.NORMAL_MAP_TYPE);
				}
			}
			
			protected function initialiseMap():void
			{
				map = new Map();
				map.sensor = "false";
				map.url = GoogleMapsConstants.SITE_URL;
				map.key = GoogleMapsConstants.APP_KEY;
				
				map.addEventListener(MapEvent.MAP_READY, handleMapReady);

				map.percentHeight = 100;
				map.percentWidth = 100;

				mapGroup.addElement(map);
				
				invalidateDisplayList();
			}
			
			protected function handleMapReady(event:MapEvent):void
			{
				map.addControl(new ScaleControl());
				map.addControl(new ZoomControl());

				isMapReady = true;
				isBusy = false;
			}
			
			protected function findMeHandler(event:MouseEvent):void
			{
				findMeButton.visible = false;
				findMeButton.includeInLayout = false;

				initialise();
			}
			
			protected function onRemove():void
			{
				if(map)
				{
					map.unload();
				}
				if(geolocation)
				{
					geolocation = null;
					geolocation.removeEventListener(GeolocationEvent.UPDATE, handleGeolocationUpdate);
				}
			}
			
		]]>
	</fx:Script>

	<s:Button id="findMeButton" 
			  label="{resourceManager.getString('explore_air_mobile', 'find_me_button_label')}" 
			  width="100%" 
			  click="findMeHandler(event)"/>
	
	<s:BusyIndicator visible="{isBusy}" includeInLayout="{isBusy}"/>
	
	<s:Label id="infoLabel"
			 text="{infoText}" 
			 visible="{!isGpsSupported}" includeInLayout="{!isGpsSupported}" />
	
	<s:Group id="mapGroup" 
			 width="100%" height="100%" 
			 visible="{isMapReady}" includeInLayout="{isMapReady}"/>	

</core:VerticalCenteredViewWithBackButton>
